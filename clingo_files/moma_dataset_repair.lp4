#const current_year=2019.

% artist(NAME, ID, BIRTHDATE, DEATHDATE)
artist(pablo_picasso, 4609, 1881, 1973).
artist(artko, 18, 0, 0).
artist(yayoi_kusama, 3315, 1929, 0).
artist(picasso, 4609, 1881, 1912).
artist(kusama, 3315, 1829, 1929).

% artwork(TITLE, ARTIST, ARTIST_ID, YEAR_PAINTED)
artwork(seated_woman, pablo_picasso, 4609, 1934).
artwork(war_and_peace, picasso, 4609, 1955).
artwork(flower, yayoi_kusama, 3315, 1953).
artwork(endless, yayoi_kusama, 3315, 1984).
artwork(accumulation, kusama, 3315, 2027).
artwork(war, picasso, 4609, 1988).
artwork(acapulco_gold, artko, 18, 1967).
%artwork(some_picasso_painting, pablo_picasso, 4609, 1911).
%artwork(made_up_name, artko, 18, 1855).


% Integrity Constraint Violation for ARTIST_ID (denial):
icv_fd(ID, NAME1, NAME2, X1, Y1, X2, Y2) :- artist(NAME1, ID, X1, Y1), artist(NAME2, ID, X2, Y2), NAME1 != NAME2.

% Make sure there's exactly one record per ARTIST_ID
del_artist(NAME1, ID, X1, Y1) ; del_artist(NAME2,ID, X2,Y2) :- icv_fd(ID,NAME1, NAME2, X1, Y1, X2, Y2).

% Artist DB Repair
new_artist(NAME,ID,BD,DD) :- artist(NAME,ID,BD,DD), not del_artist(NAME, ID, BD,DD).

% Keep atleast one artist entry per ID
:- artist(_, ID, _, _), not new_artist(_, ID, _, _).

% Integrity Constraint Violation: ARTIST_NAME or ARTIST_ID mismatch in the artworks record.
icv_aa(TITLE, ARTIST_ID) :- artwork(TITLE, ARTIST, ARTIST_ID, YEAR_PAINTED), not new_artist(ARTIST, ARTIST_ID, _, _).

% Adding Date Integrity Constraint Violation Checks: Artwork Date not within Artist's Lifetime
def_date_error(TITLE) :- artwork(TITLE, ARTIST, ARTIST_ID, YEAR), new_artist(NAME, ARTIST_ID, BIRTHDATE, DEATHDATE), YEAR > DEATHDATE, DEATHDATE != 0, DEATHDATE != -1.
def_date_error(TITLE) :- artwork(TITLE, ARTIST, ARTIST_ID, YEAR), new_artist(NAME, ARTIST_ID, BIRTHDATE, DEATHDATE), YEAR < BIRTHDATE, BIRTHDATE != -1.

% Adding Integrity Constraint Violation Check: Painting dated in the future
def_date_error(TITLE) :- artwork(TITLE, ARTIST, ARTIST_ID, YEAR), new_artist(NAME, ARTIST_ID, BIRTHDATE, DEATHDATE), YEAR > current_year.

% Artwork DB Repair
% Fixing the ARTIST_NAME/ARTIST_ID mismatch error if possible.
new_artwork(TITLE, ARTIST, ARTIST_ID, YEAR) :- icv_aa(TITLE, ARTIST_ID), artwork(TITLE, W_ARTIST, ARTIST_ID, YEAR), new_artist(ARTIST, ARTIST_ID, _, _), not def_date_error(TITLE).

% Keeping the artworks with no constraint violations
new_artwork(TITLE, ARTIST, ARTIST_ID, YEAR) :- artwork(TITLE, ARTIST, ARTIST_ID, YEAR), not icv_aa(TITLE, ARTIST_ID), not def_date_error(TITLE).
